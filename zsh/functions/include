
local target_dir=$1
local current_file=${funcstack[2]}

# If the target directory is relative, determine where the current file lives
if [[ $target_dir != /* ]]; then
    local origin="$current_file:P:h"
    target_dir="$origin/$target_dir"
fi

local -a target_files

function {
    # LOCAL_OPTIONS is set here so that we can customize options without
    # interfering with the options set by the current shell. We need to do
    # this in an anonymous function, otherwise LOCAL_OPTIONS will still be
    # during the execution of the file(s) that are sourced below.
    setopt localoptions

    # Enable GLOB_SUBST to allow filename patterns as arguments to the
    # include function. EXTENDED_GLOB is enabled so that we can use glob
    # qualifiers.
    setopt globsubst extendedglob

    # The qualifier below unsets NULL_GLOB so that this errors out when
    # nothing matches.
    target_files=( $target_dir(#q^N) )
}

for f in ${target_files[@]}; do
    if [[ -r "$f" ]]; then
        source $f
    fi
done

# vim: ft=zsh
